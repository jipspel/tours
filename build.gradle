apply plugin: 'jacoco'
apply plugin: 'java'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.antlr:antlr4-runtime:4.5'
    compile 'org.antlr:stringtemplate:4.0.2'

    compile 'commons-io:commons-io:2.4'

    testCompile 'com.github.stefanbirkner:system-rules:1.12.1'
    testCompile 'junit:junit:4.12'
}

jacoco {
    toolVersion = '0.7.5.201505241946'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task generate(type: JavaExec) {
    description 'Generates Java bytecode from Tours file: -Psrc filename'
    main = 'tours.Tours'
    classpath = sourceSets.main.runtimeClasspath
}

generate.doFirst {
    if (project.hasProperty('src')) {
        args project.src
    } else {
        throw new StopExecutionException("Usage: -Psrc filename")
    }
}

task generateAndSave(type: JavaExec) {
    description 'Generates Java bytecode from Tours file and save Java class file to a directory: -Psrc filename -Pdir directory'
    main = 'tours.Tours'
    classpath = sourceSets.main.runtimeClasspath
}

generateAndSave.doFirst {
    if (project.hasProperty('src') && project.hasProperty('dir')) {
        args (project.src, '--save', project.dir)
    } else {
        throw new StopExecutionException("Usage: -Psrc filename -Pdir directory")
    }
}

task execute(type: JavaExec) {
    description 'Executes Tours file: -Psrc filename'
    main = 'tours.Tours'
    classpath = sourceSets.main.runtimeClasspath
}

execute.doFirst {
    if (project.hasProperty('src')) {
        args (project.src, '--execute', '--no-bytecode')
    } else {
        throw new StopExecutionException("Usage: -Psrc filename")
    }
}
